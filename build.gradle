buildscript {
    repositories {
        google()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:4.1.3")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.0")
    }
}

plugins {
    id("org.jetbrains.kotlin.multiplatform").version("1.5.0")
    id("maven-publish")
}

apply(plugin: "com.android.library")

android {
    compileSdkVersion(30)

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion(21)
        targetSdkVersion(30)
        testInstrumentationRunner "androidx.benchmark.junit4.AndroidBenchmarkRunner"
    }

    externalNativeBuild {
        ndkBuild {
            path file('src/androidMain/jni/Android.mk')
        }
    }

    ndkVersion "21.3.6528147"

    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }

    sourceSets {
        androidTest {
            java.srcDir("src/androidAndroidTest/kotlin")
            manifest.srcFile("src/androidAndroidTest/AndroidManifest.xml")
        }
        main {
            java.srcDir("src/androidMain/java")
            java.srcDir("src/androidMain/kotlin")
            manifest.srcFile("src/androidMain/AndroidManifest.xml")
        }
    }
}

repositories {
    jcenter()
    mavenCentral()
    google()
    flatDir { dirs 'libs' }
}

group = "pl.makenika.graphlite"
version = "0.0.11"

kotlin {
    explicitApi()

    android {
        compilations.all {
            kotlinOptions {
                jvmTarget = "1.8"
            }
        }
        publishLibraryVariants("release")
    }

    jvm {
        compilations.all {
            kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }

    sourceSets["androidMain"].dependencies {
    }

    sourceSets["androidAndroidTest"].dependencies {
        implementation("androidx.benchmark:benchmark-junit4:1.0.0")
        implementation("androidx.test:core:1.3.0")
        implementation("androidx.test:runner:1.3.0")
        implementation("androidx.test:rules:1.3.0")
        implementation("androidx.test.ext:junit:1.1.2")
        implementation("org.jetbrains.kotlin:kotlin-test-junit")
    }

    sourceSets["commonMain"].dependencies {
        implementation("com.benasher44:uuid:0.1.0")
        api("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0-RC-native-mt")
    }

    sourceSets["commonTest"].dependencies {
        implementation("org.jetbrains.kotlin:kotlin-test-common")
        implementation("org.jetbrains.kotlin:kotlin-test-annotations-common")
        implementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.0-RC-native-mt")
    }

    sourceSets["jvmMain"].dependencies {
        implementation("org.xerial:sqlite-jdbc:3.34.0")
    }

    sourceSets["jvmTest"].dependencies {
        implementation("org.jetbrains.kotlin:kotlin-test-junit")
    }

    sourceSets {
        all {
            languageSettings.useExperimentalAnnotation("kotlin.RequiresOptIn")
        }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
            url = uri("https://maven.pkg.github.com/armatys/GraphLite")
        }
    }
}
